# Terraform

Terraform is an open-source, declarative programming language developed by HashiCorp and allows to create both on-prem and cloud resources in the form of writing code. This is also know as Infrastructure as Code (IAC). There are several IaC tools in the market today and Terraform is only one of them. Yet it is a well known and established tool and during the course of this project we only focus on this.

Hashicorp describes that:
<blockquote>
Terraform is an infrastructure as code (IaC) tool that allows you to build, change, and version infrastructure safely and efficiently. This includes both low-level components like compute instances, storage, and networking, as well as high-level components like DNS entries and SaaS features. [@hashicorpwebsite] (https://www.terraform.io/docs) 
</blockquote> 


This means that users are able to manage and provision an entire IT infrastructure using machine-readable definition files and thus allowing faster execution when configuring infrastructure, as well as enableing full traceability of changes. Terraform comes with several hundred different providers that can be used to provision infrastructure, such as Amazon Web Services (AWS), Azure, Google Cloud Platform (GCP), Kubernetes, Helm, GitHub, Splunk, DataDog, etc.

The given chapter introduces the concepts & usage of Terraform which will be needed to create the introduced MLOps Airflow deployment in an automated and tracable way. We will learn how to use Terraform to provision ressources as well as to structure a Terraform Project.


## Prerequisites

To be able to follow this tutorial, one needs to have the AWS CLI installed as well as the AWS credentials set up. Needles to say an AWS accounts needs to be present. It is also recommended to have basic knowledge of the AWS Cloud as this tutorials used the AWS infrastructure to provision cloud resources. The attached resource definitions are specified to the AWS region `eu-central-1`. It might be necessary to change accordingly if you are set in another region. 
Further, Terraform itself needs to be installed. Please refer to the corresponding sites. The scripts are run under Terraform version `v1.2.4`. Later releases might have breaking changes. One can check its installation via `terraform version`.


## Basic usage

A Terraform project is basically just a set of files in a directory containing resource definitions of cloud ressources to be created. Those Terraform files, denoted by the ending `.tf`, use Terraform's configuration language to define the specified resources. In the following example there are two definitions made: a `provider` and a `resource`. Later in this chapter we will dive deeper in the structure of the language. For now, we only need to know this script is creating a file called `hello.txt` that includes the text `"Hello, Terraform"`. It's our Terraform version of Hello World!

```bash
provider "local" {
  version = "~> 1.4"
}
resource "local_file" "hello" {
  content = "Hello, Terraform"
  filename = "hello.txt"
}
```

### terraform init

When a project is run for the first time the terraform project needs to be initialized. This is done via the `terraform init` command. Terraform scans the project files in this step and downloads any required providers needed (more details to providers in a following section). In the given example this is the local procider.

```bash
# Initializes the working directory which consists of all the configuration files
terraform init
```

![Terraform init](images/02-Terraform/terraform-init.png) 


### terraform validate

The `terraform validate` command checks the code for syntax errors. This is optional yet a way to handle initial errors or minor careless mistakes 

```bash
# Validates the configuration files in a directory
terraform validate
```
![Terraform validate](images/02-Terraform/terraform-validate.png) 

### terraform plan

The `terraform plan` command verifies what action Terraform will perform and what resources will be created. This step is basically a *dry run* of the code to be executed. It also returns the provided values and some permission attributes which have been set.

```bash
# Creates an execution plan to reach a desired state of the infrastructure
terraform plan
```

![Terraform plan](images/02-Terraform/terraform-plan.png) 


### terraform apply

The command `Terraform apply` creates the resource specified in the `.tf` files. Initially, the same output as in the `terraform plan` step is shown (hence its *dry run*). The output further states which resources are added, which will be changed, and which resources will be destroyed. After confirming the actions the resource creation will be executed.

Modifications to previously deployed ressources can be implemented by using `terraform apply` again. The output will denote that there are resources to change.

```bash
# Provision the changes in the infrastructure as defined in the plan
terraform apply 
```

![Terraform apply](images/02-Terraform/terraform-apply.png) 


### terraform destroy

To destoy all created ressouces and to delete everything we did before, there is a `terraform destroy` command.

```bash
# Deletes all the old infrastructure resources
terraform destroy
```

![Terraform destroy](images/02-Terraform/terraform-destroy.png) 


## Core Concepts

The following section will explain the core concepts and building blocks of Terraform. This will enable you to build your very first Terraform definition files.

### Providers

Terraform relies on plugins called providers to interact with Cloud providers, SaaS providers, and other APIs. Each provider adds specific resource types and/or data sources that can be managed by Terraform. For example, the `aws` provider shown below allows to specify resources related to the AWS Cloud such as S3 Buckets or EC3 Instances.

Depending on the provider it is necessary to supply it with specific parameters. The aws provier for example needs the `region` as well as username and password. If nothing is specified it will automatically pull these information from the *AWS CLI* and the credentials specified under the directory `.aws/config`. It is also a best practice to specify the version of the provider, as the providers are usually maintained and updated on a regular basis.

```bash
provider "aws" {
  region = "us-east-1"
}
```

### Resources

A *resource* is the core building block when working with Terraform. It can be a `"local_file"` such as shown in the example above, or a cloud resource such as an `"aws_instance"` on aws. The resource type is followed by the custom name of the resource in Terraform. Resource definitions are usually specified in the `main.tf`file. Each customization and setting to a ressource is done within its resource specification. The style convention when writing Terraform code states that the resource name is named in lowercase as well as it should not repeat the resource type. An example can be seen below

```bash
# Ressource type: aws_instance
# Ressource name: my-instance
resource "aws_instance" "my-instance" {
  # resource specification
  ami           = "ami-0ddbdea833a8d2f0d"
  instance_type = "t2.micro"
  
  tags = {
    Name = "my-instance"
    ManagedBy = "Terraform"
  }
}
```

### Data Sources

*Data sources* in Terraform are “read-only” resources, meaning that it is possible to get information about existing data sources but not to create or change them. They are usually used to fetch parameters needed to create resources or generally for using parameters elsewhere in Terraform configuration.

A typical example is shown below as the `"aws_ami"` data source available in the AWS provider. This data source is used to recover attributes from an existing AMI (Amazon Machine Image). The example creates a data source called `"ubuntu”` that queries the AMI registry and returns several attributes related to the located image.

```bash
data "aws_ami" "ubuntu" {
  most_recent = true
  filter {
    name   = "name"
    values = ["ubuntu/images/hvm-ssd/ubuntu-trusty-14.04-amd64-server-*"]
  }
  filter {
    name   = "virtualization-type"
    values = ["hvm"]
  }
  owners = ["099720109477"] # Canonical
}
```

Data sources and their attributes can be used in resource definitions by prepending the `data` prefix to the attribute name. The following example used the `"aws_ami"` data source within an `"aws_instace"` resource.

```bash
resource "aws_instance" "web" {
  ami = data.aws_ami.ubuntu.id 
  instance_type = "t2.micro"
}
```

## State

A Terraform state stores all details about the resources and data created within a given context. Whenever a resource is create terrafrom stores its identifier in the statefile `terraform.tfstate`. 
Providing information about already existing resources is the primary purpose of the statefile. Whenever a Terraform script is applied or whenever the resource definitions are modified, Terraform knows what to create, change, or delete based on the existing entries within the statefile. Everything specified and provisioned within Terraform will be stored in the statefile. This should be kept in mind and detain to store sensitive information such as initial passwords.

Terraform uses the concept of a backend to store and retrieve its statefile. The default backend is the local backend which means to store the statefile in the project's root folder. However, we can also configure an alternative (remote) backend to store it elsewhere. The backend can be declared within a `terraform` block in the project files. The given example stores the statefile in an AWS S3 Bucket callen `some-bucket`. Keep in mind this needs access to an AWS account and also needs the AWS provider of terraform.

```bash
terraform {
  backend "s3" {
    bucket = "some-bucket"
    key = "some-storage-key"
    region = "us-east-1"
  }
}
```
    
## Modules

A Terraform module allows to reuse resources in multiple places throughout the project. They act as a container to package resource configurations. Much like in standard programming languages, Terraform code can be organized across multiple files and packages instead of having one single file containing all the code. Wrapping code into a module not only allows to reuse it throughout the project, but also in different environments, for example when deploying a *dev* and a *prod* infrastructure. Both environments can reuse code from the same module, just with different settings.

A Terraform module is build as a directory containing one or more resource definition files. Basically, when putting all our code in a single directory, we already have a module. This is exactly what we did in our previous examples. However, terraform does not include subdirectories on its own. Subdirectories must be called explicitly using a terraform `module`parameter. The example below references a module located in a `./network` subdirectory and passes two parameters to it.

```bash
# main.tf
module "network" {
  source = "./networking"
  create_public_ip = true
  environment = "prod"
}
```

Each module consists of a similar file structure as the root directory. This includes a `main.tf` where all resources are specified, as well as files for different data sources such as `variables.tf` and `outputs.tf`. However, providers are usually configured only in the root module and are not reused in modules. Note that there are different approaches on where to specify the providers. They are either specified in the `main.tf` or a separate `providers.tf`. It does not make a difference for Terraform as it does not distinguish between the resource definition files. It is merely a strategy to keep code and project in a clean and consistent structure.

```
root
│   main.tf
│   variables.tf
│   outputs.tf
│
└── networking
    │   main.tf
    │   variables.tf
    │   outputs.tf
```


### Input Variables

Each module can have multiple *Input Variables*. Input Variables serve as parameters for a Terraform module so users can customize behavior without editing the source. In the previous example of importing a `network` module, there have been two input variables specified, `create_public_ip` and `environment`. Input variables are usually specified in the `variables.tf` file.

```bash
# variables.tf
variable "instance_name" {
  type = string
  default = "awesome-instance"
  description = "Name of the aws instance to be created"
}
```
Each variable has a type (e.g. `string`, `map`, `set`, `boolen`) and may have a `default` value and `description`. Any variable that has no default must be supplied with a value when calling the `module` reference. This means that variables defined at the root module need values assigned to as a requirement so Terraform will not fail. This can be done by different resources, for example 

* a variable's `default` value
* via the command line using the `terraform apply -var="variable=value"`option
* via environment variables starting with `TF_VAR_`; Terraform will check them automatically
* a `.tfvars` file where the variable values are specified; Terraform can load variable definitions from these files automatically (please check online resources for further insights)

Variables can be used in expressions using the `var.`prefix such as shown in below example. We use the resource configuration of the previous example to create an `aws_instance` but this time its name is provided by an input variable.

```bash
# main.tf
resource "aws_instance" "awesome-instance" {
  ami           = "ami-0ddbdea833a8d2f0d"
  instance_type = "t2.micro"
  
  tags = {
    Name = var.instance_name
  }
}
```


### Output Variables

Similar to Input variables, a terraform module has *output variables*. As their name states, output variables return values of a Terraform module and are denoted in the *outputs.tf* file as expected. A module's consumer has no access to any resources or data created within the module itself. However, sometimes a modules attrivutes are needed for another module or resource. Output variables address this issue by exposing a defined subset of the created resources.

The example below defines an output value *instance_address* containing the IP address of an EC2 instance the we create with a module. Any module that reference this module can use the *instance_address* value by referencing it via *module.module_name.instance_address*

```bash
# outputs.tf
output "instance_address" {
      value = aws_instance.awesome-instance.private_ip
      description = "Web server's private IP address"
    }
```

![outputs](images/02-Terraform/outputs.png "outputs") 

### Local Variables

Additionally to Input variables and output variables a module provides the use of local variables. Local values are basically just a convenience feature to assign a shorter name to an expression and work like standard variables. This means theor scope is also limited to the module they are declared in. Using local variables reduces code repetitions which can be especially valuable when dealing with output variables from a module. 

```bash
# main.tf
locals {
  vpc_id = module.network.vpc_id
}
module "network" {
  source = "./network"
}
module "service1" {
  source = "./service1"
  vpc_id = local.vpc_id
}
module "service2" {
  source = "./service2"
  vpc_id = local.vpc_id
}
```

## Additional tips & tricks

Of course there is much more to Terraform than these small examples can provide. Yet there are also some contrains when working with Terraform or declarative languages in general. Typically they do not have  for-loops or other traditional procedural logic built into the language to repeat a piece of logic or conditional if-statements to configure reasources on demand. However, there are ways there are some ways to deal with this issue and to create multiple respurces without copy and paste.

Terraform comes with different looping constructs, each used slightly different. The *count* and *for_each* meta arguments enable us to create multiple instances of a resource. 

### count

Count can be used to loop over any resource and module. Every Terraform resource has a meta-parameter *count* one can use. Count is the simplest, and most limited iteration construct and all it does is to define how many copies to create of a resource. When creating multiple instance with one specification, the problem is that each instance must have a unique name, otherwise Terraform would cause an error. Therefore we need to index the meta-parameter just like doing it in a for-loop to give each resource a unique name. The example below shows how to do this on an AWS IAM user.

```bash
resource "aws_iam_user" "example" {
  count = 2
  name  = "neo.${count.index}"
}
```

![count](images/02-Terraform/additionals_count.png) 


```bash
variable "user_names" {
  description = "Create IAM users with these names"
  type        = list(string)
  default     = ["adam", "eve", "snake", "apple"]
}

resource "aws_iam_user" "example" {
  count = length(var.user_names) # returns the number of items in the given array
  name  = var.user_names[count.index]
}
```

![count list](images/02-Terraform/additionals_count_list.png) 

After using count on a resource it becomes an array of resources rather than one single resource. The same hold when using count on modules. When adding count to a module it turns it into an array of modules.

This can round into problems because the way Terraform identifies each resource within the array is by its index. Now, when removing an item from the middle of the array, all items after it shift one index back. This will result in Terraform deleting every resource after that item and then re-creating these resources again from scratch

So after running `terraform plan` with just three names, Terraform’s internal representation will look like this:
```bash
variable "user_names" {
  description = "Create IAM users with these names"
  type        = list(string)
  default     = ["adam", "eve", "apple"]
}

resource "aws_iam_user" "example" {
  count = length(var.user_names) # returns the number of items in the given array
  name  = var.user_names[count.index]
}
```

![count index deletion](images/02-Terraform/additionals_count_index_deletion.png)

**Count as conditional**
Count can also be used as a form of a conditional if statement. This is possible as Terraform  supports *conditional expressions*. If `count` is set to one 1, one copy of that resource is created; if set to 0, the resource is not created at all. Writing this as a conditional expression could look something like the follow, where var.enable_autoscaling is a boolean variable either set to `True` or `False`.

```bash
resource "example-1" "example" {
  count = var.enable_autoscaling ? 1 : 0
  name  = var.user_names[count.index]
}
```

### for-each

The *for_each* expression allows to loop over lists, sets, and maps to create multiple copies of a resource just like the *count* meta. The main difference between them is that *count* expects a non-negative number, whereas *for_each* only accepts a list or map of values. Using the same example as above it would look like this:

```bash
variable "user_names" {
  description = "Create IAM users with these names"
  type        = list(string)
  default     = ["adam", "eve", "snake", "apple"]
}

resource "aws_iam_user" "example" {
  for_each = toset(var.user_names)
  name     = each.value
}

output "all_users" {  
  value = aws_iam_user.example  
}
```

<<<<<<< HEAD
![for each on list](images/02-Terraform/additionals_for_each_on_list.png)
=======
![for each](images/02-Terraform/additionals_for_each.png)

>>>>>>> ab16f8b8832cb3f4c8851aa0fafa06fff554647a

Using a map of resource with the *for_each* meta rather than an array of resources as with *count* has the benefit to remove items from the middle of the collection safely and without re-creating the resources following the deleted item. Of course, the same can also be done for modules.

```bash
module "users" {  
  source = "./iam-user"  
  
  for_each  = toset(var.user_names)  
  user_name = each.value  
}
```


### for

Terraform also offers a similar functionality as python list comprehension in the form of a for expression. This should not be confused with the *for-each* expression seen above. The basic syntax is shown below to convert the list of names of previous examples in var.names to uppercase:

```bash
output "upper_names" {
  value = [for name in var.names : upper(name)]
}

output "short_upper_names" {
  value = [for name in var.names : upper(name) if length(name) < 5]
}
```

![for on list](images/02-Terraform/additionals_for_on_list.png) 

Using for to loop over lists and maps within a string can be used similarly. This allows us to use control statements directly withing strings using a syntax similar to string interpolation.

```bash
output "for_directive" {  
  value = "%{ for name in var.names }${name}, %{ endfor }"  
}
```

![Bild nicht gefunden: images/02-Terraform/additionals_for_on_string.png](images/02-Terraform/additionals_for_on_string.png "Bild nicht gefunden: images/02-Terraform/additionals_for_on_string.png") 


### Workspaces

Terraform workspaces allow us to keep multiple state files for the same project. When we run Terraform for the first time in a project, the generated state file will go into the *default* workspace. Later, we can create a new workspace with the `terraform workspace new` command, optionally supplying an existing state file as a parameter.

![workspaces](images/02-Terraform/additionals_workspaces.png)


## Putting it together

**Building a VPC with EC2 & S3**

Finally, we want to put everything together and provision our own cloud infrastructure using Terraform. We will create a AWS Virtual private Cloud with EC2 Instances running on it, and S3 Buckets attached to the them. We will use *for_each* and *count* to create multiple instaces. The Terraform infrastructure is separated into three modules *VPS*, *EC2*, and *S3*.

```bash
root
│   main.tf
│   variables.tf
│   outputs.tf
│
└── networking
│   │   main.tf
│   │   variables.tf
│   │   outputs.tf
│
└── ec2
│   │   main.tf
│   │   variables.tf
│   │   outputs.tf
│
└── s3
    │   main.tf
    │   variables.tf
    │   outputs.tf
```
